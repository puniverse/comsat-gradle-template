buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-cargo-plugin:2.0.3'
    }
}

apply plugin: 'java'

apply from: 'gradle/props.gradle'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

configurations {
    quasar
    provided
}

sourceSets {
    main.compileClasspath = main.compileClasspath + configurations.provided
    test.compileClasspath = test.compileClasspath + configurations.provided
    test.runtimeClasspath = test.runtimeClasspath + configurations.provided
}

configurations.all {
    resolutionStrategy {
        failOnVersionConflict()
        force "com.google.guava:guava:$guavaVer"
        force "com.fasterxml.jackson.core:jackson-core:$jacksonVer"
        force "com.fasterxml.jackson.core:jackson-databind:$jacksonVer"
        force "com.fasterxml.jackson.core:jackson-annotations:$jacksonVer"
        force "com.fasterxml.jackson.jaxrs:jackson-jaxrs-base:$jacksonVer"
        force "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:$jacksonVer"

        force "xml-apis:xml-apis:1.3.03"
    }
}

repositories {
//    mavenLocal()
    mavenCentral()
//    maven { url "https://oss.sonatype.org/content/repositories/releases" }
//    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
//    maven { url 'https://maven.java.net/content/repositories/snapshots' }
}

test {
    beforeTest { desc ->
        logger.quiet("Running test: " + desc)
    }

    afterTest { desc, result ->
        if(result.resultType == TestResult.ResultType.FAILURE) {
            logger.quiet("Failed test ${desc.name} [${desc.className}] with exception: ${result.exception}")
            if(result.exception != null) {
                result.exception.printStackTrace()
            }
        }
    }
}

dependencies {
    compile "com.h2database:h2:$h2Ver"
    compile "com.squareup.dagger:dagger:$daggerVer"
    compile "com.squareup.dagger:dagger-compiler:$daggerVer"
    compile "co.paralleluniverse:comsat-jersey-server:$comsatVer"
    compile "com.netflix.feign:feign-core:$feignVer"
    compile ("com.netflix.feign:feign-jaxrs:$feignVer") {
        exclude group: 'javax.ws.rs', module: 'jsr311-api'
    }
    compile ("com.netflix.feign:feign-jackson:$feignVer") {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }
    compile "org.glassfish.jersey.media:jersey-media-json-jackson:$jerseyVer"
    // Exclude Jersey 1.x's runtime by default, Tomcat uses 2.x
    compile "co.paralleluniverse:comsat-jdbi:$comsatVer"
    compile ("co.paralleluniverse:comsat-dropwizard:$comsatVer") {
        exclude group: 'com.sun.jersey', module: '*'
    }
    compile ("io.dropwizard:dropwizard-core:$dropwizardVer") {
        exclude group: 'com.sun.jersey', module: '*'
    }
    compile "co.paralleluniverse:quasar-core:$quasarVer"
    compile "co.paralleluniverse:comsat-jdbc:$comsatVer"

    provided "javax.servlet:javax.servlet-api:$servletVer"

    quasar "co.paralleluniverse:quasar-core:${quasarVer}${classifier}@jar"

    runtime fileTree(dir: 'lib', include: '*.jar')

    testCompile "junit:junit:$junitVer"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

defaultTasks 'compile'

if (project.hasProperty('env')) {
    apply from: "gradle/${project.getProperty('env')}.gradle"
} else {
    apply from: "gradle/tomcat8.gradle"
}
